import net.darkhax.curseforgegradle.TaskPublishCurseForge

import java.nio.file.Files
import java.nio.file.StandardCopyOption

plugins {
    id 'multiloader-loader'
    id 'com.modrinth.minotaur'
    id 'net.darkhax.curseforgegradle'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
}

def refmapfilename = "${mod_id}.refmap.json";

mixin {
    add(sourceSets.main, refmapfilename)

    config("${mod_id}.mixins.json")
}

minecraft {
    mappings channel: 'official', version: minecraft_version

    reobf = false

    copyIdeResources = true

    // Automatically enable forge AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformer = at
    }

    runs {
        client {
            workingDirectory file('runs/client')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName "Client"

            property 'forge.enabledGameTestNamespaces', mod_id

            mods {
                modClientRun {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory file('runs/server')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName "Server"

            property 'forge.enabledGameTestNamespaces', mod_id

            mods {
                modServerRun {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory file('runs/data')
            ideaModule "${rootProject.name}.${project.name}.main"
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            taskName "Data"

            mods {
                modDataRun {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"
}


// Needed for fixing the class not found issue on runs
sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}

processResources {
    doFirst {
        File refmapping_file = file("build/tmp/compileJava/${refmapfilename}");
        if (refmapping_file.exists()) {
            String work_dir = "build/tmp/build_working";
            Directory dir = project.layout.projectDirectory.dir(work_dir);
            dir.getAsFile().mkdir();
            Files.copy(refmapping_file.toPath() , dir.file(refmapfilename).getAsFile().toPath() , StandardCopyOption.REPLACE_EXISTING);
            sourceSets.main.resources.srcDir(work_dir);
        }
    }
}

apply from: rootProject.file('repositories.gradle')
apply from: 'dependencies.gradle'

tasks.register('curseforge', TaskPublishCurseForge) {
    dependsOn(jar)
    description = 'Publishes the Forge build to CurseForge.'
    group = 'publishing'

    apiToken = project.findProperty("curseforge.api_key") ?: System.getenv("CURSEFORGE_TOKEN") ?: "none"

    def mainFile = it.upload(curseforge_project_id, project.layout.buildDirectory.file("libs/${base.archivesName.get()}-${version}.jar").get().getAsFile())
    mainFile.changelog = rootProject.file('CHANGELOG.md').text
    mainFile.addRequirement("mdcdi1315-base-lib")
    project.minecraft_versions.split(',').toList().each { mainFile.addGameVersion(it) }
    mainFile.releaseType = curseforge_release_type
    mainFile.addModLoader("Forge")
}

modrinth {
    //token = project.findProperty("modrinth.token") ?: System.getenv("MODRINTH_TOKEN") ?: "none"
    projectId = modrinth_project_id
    versionType = modrinth_release_type
    versionNumber = project.version + "+forge-" + project.minecraft_version
    uploadFile = jar
    changelog = rootProject.file("CHANGELOG.md").text
    gameVersions = project.minecraft_versions.split(',').toList()
    syncBodyFrom = rootProject.file("modpage.md").text
    loaders = ['forge']
    dependencies {
        required.project "mdcdi1315-base-lib"
    }
}

tasks.modrinth.dependsOn(jar)