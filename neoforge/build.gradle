import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
    id 'com.modrinth.minotaur'
    id 'net.darkhax.curseforgegradle'
}

base {
    archivesName = "${mod_id}-neoforge-${minecraft_version}-${release_cycle}"
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.add(at.absolutePath)
    }

    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }

    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})"
        }

        client {
            client()
        }

        server {
            server()
        }

        data {
            data()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

apply from: rootProject.file('repositories.gradle')
apply from: 'dependencies.gradle'

tasks.register('curseforge', TaskPublishCurseForge) {
    dependsOn(jar)
    description = 'Publishes the NeoForge build to CurseForge.'
    group = 'publishing'

    apiToken = project.findProperty("curseforge.api_key") ?: System.getenv("CURSEFORGE_TOKEN") ?: "none"

    def mainFile = it.upload(curseforge_project_id, project.layout.buildDirectory.file("libs/${base.archivesName.get()}-${version}.jar").get().getAsFile())
    mainFile.changelog = rootProject.file('CHANGELOG.md').text
    project.minecraft_versions.split(',').toList().each { mainFile.addGameVersion(it) }
    mainFile.releaseType = curseforge_release_type
    mainFile.addModLoader("NeoForge");
    mainFile.addRequirement("mdcdi1315-base-lib")
}

modrinth {
    token = project.findProperty("modrinth.token") ?: System.getenv("MODRINTH_TOKEN") ?: "none"
    projectId = modrinth_project_id
    versionType = modrinth_release_type
    versionNumber = project.version + "+neoforge-" + project.minecraft_version
    uploadFile = jar
    changelog = rootProject.file("CHANGELOG.md").text
    gameVersions = project.minecraft_versions.split(',').toList()
    syncBodyFrom = rootProject.file("modpage.md").text
    loaders = ['neoforge']
    dependencies {
        required.project "mdcdi1315-base-lib"
        required.project "fabric-api"
    }
}