import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'idea'
    id 'fabric-loom'
    id 'maven-publish'
    id 'multiloader-loader'
    id 'com.modrinth.minotaur'
    id 'net.darkhax.curseforgegradle'
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"

    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

apply from: rootProject.file('repositories.gradle')
apply from: 'dependencies.gradle'

loom {

    def aw = project(":platform_agnostic").file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
        data {
            inherit client
            setConfigName("Fabric Data")
            ideConfigGenerated(true)
            runDir("build/datagen")

            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${project(":platform_agnostic").file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

tasks.register('curseforge', TaskPublishCurseForge) {
    dependsOn(remapJar)
    description = 'Publishes the Fabric build to CurseForge.'
    group = 'publishing'

    apiToken = project.findProperty("curseforge.api_key") ?: System.getenv("CURSEFORGE_TOKEN") ?: "none"

    def mainFile = it.upload(curseforge_project_id, project.layout.buildDirectory.file("libs/${base.archivesName.get()}-${version}.jar").get().getAsFile())
    mainFile.changelog = rootProject.file('CHANGELOG.md').text
    mainFile.addRequirement("fabric-api")
    mainFile.addRequirement("mdcdi1315-base-lib")
    project.minecraft_versions.split(',').toList().each { mainFile.addGameVersion(it) }
    mainFile.releaseType = curseforge_release_type
    mainFile.addModLoader("Fabric")
}

modrinth {
    //token = project.findProperty("modrinth.token") ?: System.getenv("MODRINTH_TOKEN") ?: "none"
    projectId = modrinth_project_id
    versionType = modrinth_release_type
    versionNumber = project.version + "+fabric-" + project.minecraft_version
    uploadFile = remapJar
    changelog = rootProject.file("CHANGELOG.md").text
    gameVersions = project.minecraft_versions.split(',').toList()
    syncBodyFrom = rootProject.file("modpage.md").text
    loaders = ['fabric']
    dependencies {
		required.project "mdcdi1315-base-lib"
        required.project "fabric-api"
    }
}

tasks.modrinth.dependsOn(remapJar)